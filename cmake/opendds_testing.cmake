function(_opendds_add_test)
  set(no_value_options)
  set(single_value_options NAME)
  set(multi_value_options COMMAND)
  cmake_parse_arguments(arg
    "${no_value_options}" "${single_value_options}" "${multi_value_options}" ${ARGN})

  set(test_name "${PROJECT_NAME}_test")
  if(arg_NAME)
    set(test_name "${test_name}_${arg_NAME}")
  endif()
  add_test(NAME "${test_name}" ${arg_UNPARSED_ARGUMENTS}
    COMMAND ${arg_COMMAND} $<$<BOOL:$<CONFIG>>:-ExeSubDir> $<CONFIG>)
  set(env "ACE_ROOT=${ACE_ROOT}" "TAO_ROOT=${TAO_ROOT}")
  if(MSVC)
    set(env_var_name PATH)
  else()
    set(env_var_name LD_LIBRARY_PATH)
  endif()
  _opendds_path_list(lib_dir_list
    "$ENV{${env_var_name}}" "${TAO_LIB_DIR}" "${OPENDDS_LIB_DIR}$<$<BOOL:$<CONFIG>>:/$<CONFIG>>")
  if(WIN32)
    string(REPLACE "/" "\\" lib_dir_list "${lib_dir_list}")
    string(REPLACE ";" "\;" lib_dir_list "${lib_dir_list}")
  endif()
  list(APPEND env "${env_var_name}=${lib_dir_list}")
  if(DEFINED OPENDDS_BUILD_DIR)
    list(APPEND env "OPENDDS_BUILD_DIR=${OPENDDS_BUILD_DIR}")
  endif()
  if(DEFINED OPENDDS_SOURCE_ROOT)
    _opendds_path_list(perl5lib "${OPENDDS_SOURCE_ROOT}/bin" "${ACE_ROOT}/bin")
    list(APPEND env "PERL5LIB=${perl5lib}")
  endif()
  if(env)
    set_property(TEST "${test_name}" PROPERTY ENVIRONMENT "${env}")
  endif()
endfunction()
