function(_opendds_add_test)
  set(no_value_options)
  set(single_value_options NAME)
  set(multi_value_options)
  cmake_parse_arguments(arg
    "${no_value_options}" "${single_value_options}" "${multi_value_options}" ${ARGN})

  set(test_name "${PROJECT_NAME}_test")
  if(arg_NAME)
    set(test_name "${test_name}_${arg_NAME}")
  endif()
  add_test(NAME "${test_name}" ${arg_UNPARSED_ARGUMENTS})
  set(env "ACE_ROOT=${ACE_ROOT}" "TAO_ROOT=${TAO_ROOT}")
  if(DEFINED OPENDDS_BUILD_DIR)
    list(APPEND env "OPENDDS_BUILD_DIR=${OPENDDS_BUILD_DIR}")
  endif()
  if(DEFINED OPENDDS_SOURCE_ROOT)
    list(APPEND env "PERL5LIB=${OPENDDS_SOURCE_ROOT}/bin:${ACE_ROOT}/bin")
  endif()
  if(env)
    set_property(TEST "${test_name}" PROPERTY ENVIRONMENT "${env}")
  endif()
endfunction()
